apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId 'com.crakac.ofuton'
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 30
        versionName '2.1.0'
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            minifyEnabled false
        }
        applicationVariants.all { variant ->
            def twitterProps = getProps("app/twitter.properties")
            variant.buildConfigField "String", "TWITTER_API_KEY", "\""+twitterProps["key"]+"\""
            variant.buildConfigField "String", "TWITTER_API_SECRET", "\""+twitterProps["secret"]+"\""
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'AndroidManifest.xml'
        exclude 'LICENSE.txt'
    }
    productFlavors {
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    mavenCentral()
}


dependencies {
    ext.support_lib_version = "27.0.2"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':volley')
    implementation "com.android.support:support-annotations:$support_lib_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:exifinterface:$support_lib_version"
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.github.esafirm.android-image-picker:imagepicker:1.9.2"
}

static def getProps(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File(key)))
    return props
}
